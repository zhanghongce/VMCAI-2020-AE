# ---------------------------------------------------------------------------- #
# define dependencies
# ---------------------------------------------------------------------------- #
include(CMakeFindDependencyMacro)

find_dependency(Z3 REQUIRED)
find_dependency(Boost 1.50 REQUIRED COMPONENTS @BOOST_PY_COMP@)

if(${CMAKE_VERSION} VERSION_LESS 3.12)
  find_dependency(PythonLibs 2.7 REQUIRED)

  if(NOT TARGET Python::Python)

    add_library(Python::Python IMPORTED INTERFACE)

    set_property(TARGET Python::Python PROPERTY
      INTERFACE_INCLUDE_DIRECTORIES ${PYTHON_INCLUDE_DIRS}
    )

    set_property(TARGET Python::Python PROPERTY
      INTERFACE_LINK_LIBRARIES ${PYTHON_LIBRARIES}
    )

  endif() # target Python::Python

else()
  find_dependency(Python 2.7 REQUIRED COMPONENTS Development)
endif()

# ---------------------------------------------------------------------------- #
# define exports
# ---------------------------------------------------------------------------- #
include(FindPackageHandleStandardArgs)

set(${CMAKE_FIND_PACKAGE_NAME}_CONFIG ${CMAKE_CURRENT_LIST_FILE})

find_package_handle_standard_args(@PROJECT_NAME@ CONFIG_MODE)

if(NOT TARGET @PROJECT_NAME@::@ILASYNTH_TARGET_NAME@)
  include("${CMAKE_CURRENT_LIST_DIR}/@ILASYNTH_TARGETS_EXPORT_NAME@.cmake")

  if((NOT TARGET @ILASYNTH_TARGET_NAME@) AND 
     (NOT @PROJECT_NAME@_FIND_VERSION OR 
      @PROJECT_NAME@_FIND_VERSION VERSION_LESS 1.0.0))

    add_library(@ILASYNTH_TARGET_NAME@ INTERFACE IMPORTED)

    set_target_properties(@ILASYNTH_TARGET_NAME@ PROPERTIES
      INTERFACE_LINK_LIBRARIES @PROJECT_NAME@::@ILASYNTH_TARGET_NAME@
    )

  endif()

endif()

